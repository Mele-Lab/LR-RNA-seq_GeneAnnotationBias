import pandas as pd

include: 'blast.smk'
include: 'samtools.smk'
include: 'formatting.smk'

configfile: 'config.yml'

df = pd.read_csv('test_data.tsv', sep='\t')

def get_df_val(df, col, sample):
    temp = df.loc[df['sample']==sample]
    assert len(temp.index) == 1
    return temp[col].values[0]

rule all:
    input:
        expand(config['data']['blast']['xml'],
               sample=df['sample'].tolist())

# corr. to 00_fastq2fasta.sh
use rule fq_to_fa as raw_fq_to_fa with:
    input:
        fq = lambda wc: get_df_val(df, 'fname', wc.sample)
        # fq = config['raw']['fq']
    output:
        fa = config['data']['raw']['fa']

# corr. to 01_blast/00_create_db.sh
use rule make_blast_db as blast_db with:
    input:
        fa = config['ref']['linker_5_fa']
    params:
        db = config['data']['blast']['db']
    output:
        nhr = config['data']['blast']['nhr'],
        nin = config['data']['blast']['nin'],
        nsq = config['data']['blast']['nsq']

# corr to 01_blast/01_blast.sh
use rule blast as blast_seq with:
    input:
        fa = config['data']['raw']['fa'],
        nhr = config['data']['blast']['nhr']
    params:
        db = config['data']['blast']['db']
    output:
        xml = config['data']['blast']['xml']

# corr to 01_blast/02_xml2sam
use rule blast2bam as blast2bam_seq with:
    input:
        ref_fa = config['ref']['fa'],
        fa = config['data']['fa'],
        xml = config['data']['blast']['xml']
    output:
        sam = temporary(config['data']['blast']['sam'])

use rule sam_filt_unmapped as filt_sam_blast with:
    input:
        align = config['data']['blast']['sam']
    output:
        align = temporary(config['data']['blast']['sam_filt'])

use rule sam_to_bam as sam_to_bam_blast with:
    input:
        align = config['data']['blast']['sam_filt']
    output:
        align = temporary(config['data']['blast']['bam'])

use rule sam_sort as bam_sort_blast with:
    input:
        align = config['data']['blast']['bam']
    output:
        align = config['data']['blast']['bam_sort']

use rule bam_index as bam_index_blast with:
    input:
        align = config['data']['blast']['bam_sort']
    output:
        align = config['data']['blast']['bam_ind']
