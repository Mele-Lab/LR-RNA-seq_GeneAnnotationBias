import pandas as pd

include: 'blast.smk'
include: 'samtools.smk'
include: 'formatting.smk'

configfile: 'config.yml'

df = pd.read_csv('test_data.tsv', sep='\t')

def get_df_val(df, col, sample):
    temp = df.loc[df['sample']==sample]
    assert len(temp.index) == 1
    return temp[col].values[0]

rule all:
    input:
        expand(rules.blast_seq.output,
               sample=df['sample'].tolist())

# corr. to 00_fastq2fasta.sh
use rule fq_to_fa as raw_fq_to_fa with:
    input:
        fq = lambda wc: get_df_val(df, 'fname', wc.sample)
        # fq = config['raw']['fq']
    output:
        fa = config['raw']['fa']

# corr. to 01_blast/00_create_db.sh
use rule make_blast_db as blast_db with:
    input:
        fa = config['ref']['fa']
    output:
        db = config['data']['blast']['db']

# corr to 01_blast/01_blast.sh
use rule blast as blast_seq with:
    input:
        fa = rules.raw_fq_to_fa.output.fa,
        db = rules.blast_db.output.db
    output:
        xml = config['data']['blast']['xml']
